# Try to find the SDL2 library
#  SDL2_FOUND - system has SDL2
#  SDL2_INCLUDE_DIR - the SDL2 include directory
#  SDL2_LIBRARY - the SDL2 library

FIND_PATH(SDL2_INCLUDE_DIR PATH_SUFFIXES SDL2 SDL NAMES SDL_opengles2.h)
SET(_SDL2_STATIC_LIBS libSDL2.a)
SET(_SDL2_SHARED_LIBS libSDL2.dll.a SDL2)
IF(USE_STATIC_LIBS)
    FIND_LIBRARY(SDL2_LIBRARY NAMES ${_SDL2_STATIC_LIBS} ${_SDL2_SHARED_LIBS})
ELSE()
    FIND_LIBRARY(SDL2_LIBRARY NAMES ${_SDL2_SHARED_LIBS} ${_SDL2_STATIC_LIBS})
ENDIF()
SET(SDL2_LIBRARIES ${SDL2_LIBRARY})
IF(APPLE)
    FIND_PACKAGE(Iconv)
    IF(ICONV_FOUND)
        SET(SDL2_LIBRARIES ${SDL2_LIBRARIES} ${ICONV_LIBRARY})
    ENDIF()
    FIND_LIBRARY(FOUNDATION_LIBRARY Foundation)
    FIND_LIBRARY(COCOA_LIBRARY Cocoa)
    FIND_LIBRARY(IOKIT_LIBRARY IOKit)
    FIND_LIBRARY(CARBON_LIBRARY Carbon)
    SET(SDL2_LIBRARIES ${SDL2_LIBRARIES} ${FOUNDATION_LIBRARY} ${COCOA_LIBRARY} ${IOKIT_LIBRARY} ${CARBON_LIBRARY})
ELSEIF(WIN32)
    SET(SDL2_LIBRARIES ${SDL2_LIBRARIES} imm32 winmm version)
ENDIF()
INCLUDE(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(SDL2 DEFAULT_MSG SDL2_LIBRARY SDL2_LIBRARIES SDL2_INCLUDE_DIR)
MARK_AS_ADVANCED(SDL2_LIBRARY SDL2_LIBRARIES SDL2_INCLUDE_DIR)
